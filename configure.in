dnl Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.am)

#
# Making releases:
#   CPPUNIT_MICRO_VERSION += 1;
#   CPPUNIT_INTERFACE_AGE += 1;
#   CPPUNIT_BINARY_AGE += 1;
# if any functions have been added, set CPPUNIT_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set CPPUNIT_BINARY_AGE and CPPUNIT_INTERFACE_AGE to 0.
#
CPPUNIT_MAJOR_VERSION=1
CPPUNIT_MINOR_VERSION=5
CPPUNIT_MICRO_VERSION=5
CPPUNIT_INTERFACE_AGE=1
CPPUNIT_BINARY_AGE=1
CPPUNIT_VERSION=$CPPUNIT_MAJOR_VERSION.$CPPUNIT_MINOR_VERSION.$CPPUNIT_MICRO_VERSION
AC_SUBST(CPPUNIT_MAJOR_VERSION)
AC_SUBST(CPPUNIT_MINOR_VERSION)
AC_SUBST(CPPUNIT_MICRO_VERSION)
AC_SUBST(CPPUNIT_INTERFACE_AGE)
AC_SUBST(CPPUNIT_BINARY_AGE)
AC_SUBST(CPPUNIT_VERSION)

# libtool versioning
LT_RELEASE=$CPPUNIT_MAJOR_VERSION.$CPPUNIT_MINOR_VERSION
LT_CURRENT=`expr $CPPUNIT_MICRO_VERSION - $CPPUNIT_INTERFACE_AGE`
LT_REVISION=$CPPUNIT_INTERFACE_AGE
LT_AGE=`expr $CPPUNIT_BINARY_AGE - $CPPUNIT_INTERFACE_AGE`
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(include/config.h)
AM_INIT_AUTOMAKE(cppunit, $CPPUNIT_VERSION)

# General "with" options
# ----------------------------------------------------------------------------
AC_ARG_ENABLE(doxygen, [  --enable-doxygen        enable documentation generation with doxygen (auto)])
AC_ARG_ENABLE(dot, [  --enable-dot            use 'dot' to generate graphs in doxygen (auto)])              
AC_ARG_ENABLE(html-docs, [  --enable-html-docs      enable HTML generation with doxygen (yes)], [], [ enable_html_docs=yes])              
AC_ARG_ENABLE(latex-docs, [  --enable-latex-docs     enable LaTeX documentation generation with doxygen (no)], [], [ enable_latex_docs=no])              


dnl Checks for programs.

AM_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_PROG_CXX
AC_LANG_CPLUSPLUS

AC_ARG_ENABLE(debug-mode,   
[  --enable-debug-mode     enable debugging mode],
  [
    CFLAGS="-D_DEBUG -g3 -Wall"
    CXXFLAGS="-D_DEBUG -g3 -Wall"
  ],[
    CFLAGS="-O2 "
    CXXFLAGS="-O2"
  ]) 

# check for doxygen
# ----------------------------------------------------------------------------
if test "x$enable_doxygen" = xno; then
        enable_doc=no
else 
        AC_PATH_PROG(DOXYGEN, doxygen, , $PATH)
        if test x$DOXYGEN = x; then
                if test "x$enable_doxygen" = xyes; then
                        AC_MSG_ERROR([could not find doxygen])
                fi
                enable_doc=no
        else
                enable_doc=yes
                AC_PATH_PROG(DOT, dot, , $PATH)
        fi
fi
AM_CONDITIONAL(DOC, test x$enable_doc = xyes)

if test x$DOT = x; then
        if test "x$enable_dot" = xyes; then
                AC_MSG_ERROR([could not find dot])
        fi
        enable_dot=no
else
        enable_dot=yes
fi
AC_SUBST(enable_dot)
AC_SUBST(enable_html_docs)
AC_SUBST(enable_latex_docs)


AC_OUTPUT([
  Makefile 
  cppunit.spec
  cppunit-config
  config/Makefile
  src/Makefile
  src/cppunit/Makefile
  include/Makefile
  include/cppunit/Makefile
  include/cppunit/extensions/Makefile
  doc/Makefile
  doc/Doxyfile
  examples/Makefile
  examples/hierarchy/Makefile
],[chmod a+x cppunit-config])
