dnl Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.am)

# autoconf 2.50 or higher to rebuild aclocal.m4, because the
# AC_CREATE_PREFIX_CONFIG_H macro needs the AS_DIRNAME macro.
AC_PREREQ(2.50)

#
# Making releases:
#   CPPUNIT_MICRO_VERSION += 1;
#   CPPUNIT_INTERFACE_AGE += 1;
#   CPPUNIT_BINARY_AGE += 1;
# if any functions have been added, set CPPUNIT_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set CPPUNIT_BINARY_AGE and CPPUNIT_INTERFACE_AGE to 0.
#
CPPUNIT_MAJOR_VERSION=1
CPPUNIT_MINOR_VERSION=5
CPPUNIT_MICRO_VERSION=5
CPPUNIT_INTERFACE_AGE=0
CPPUNIT_BINARY_AGE=0
CPPUNIT_VERSION=$CPPUNIT_MAJOR_VERSION.$CPPUNIT_MINOR_VERSION.$CPPUNIT_MICRO_VERSION
AC_SUBST(CPPUNIT_MAJOR_VERSION)
AC_SUBST(CPPUNIT_MINOR_VERSION)
AC_SUBST(CPPUNIT_MICRO_VERSION)
AC_SUBST(CPPUNIT_INTERFACE_AGE)
AC_SUBST(CPPUNIT_BINARY_AGE)
AC_SUBST(CPPUNIT_VERSION)

# libtool versioning
LT_RELEASE=$CPPUNIT_MAJOR_VERSION.$CPPUNIT_MINOR_VERSION
LT_CURRENT=`expr $CPPUNIT_MICRO_VERSION - $CPPUNIT_INTERFACE_AGE`
LT_REVISION=$CPPUNIT_INTERFACE_AGE
LT_AGE=`expr $CPPUNIT_BINARY_AGE - $CPPUNIT_INTERFACE_AGE`
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config/config.h)
AM_INIT_AUTOMAKE(cppunit, $CPPUNIT_VERSION)

# General "with" options
# ----------------------------------------------------------------------------

dnl Checks for programs.

# The libtool macro AM_PROG_LIBTOOL checks for the C preprocessor.
# Configure gets confused if we try to check for a C preprocessor
# without first checking for the C compiler
# (see http://sources.redhat.com/ml/autoconf/2001-07/msg00036.html),
# so we invoke AC_PROG_CC explicitly.
AC_PROG_CC
AC_PROG_CXX
AC_LANG(C++)

AM_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_ARG_ENABLE(debug-mode,   
[  --enable-debug-mode     enable debugging mode],
  [
    CFLAGS="-D_DEBUG -g3 -Wall"
    CXXFLAGS="-D_DEBUG -g3 -Wall"
  ],[
    CFLAGS="-O2 "
    CXXFLAGS="-O2"
  ]) 

# check for doxygen
BB_ENABLE_DOXYGEN

# Check for compiler characteristics 
# ----------------------------------------------------------------------------

AC_CXX_RTTI
AC_CXX_STRING_COMPARE_STRING_FIRST

if test "$ac_cv_cxx_rtti" = yes; then
    CPPUNIT_USE_TYPEINFO=1
else
    CPPUNIT_USE_TYPEINFO=0
fi
AC_SUBST(CPPUNIT_USE_TYPEINFO)


AC_OUTPUT([
  Makefile 
  cppunit.spec
  cppunit-config
  config/Makefile
  src/Makefile
  src/cppunit/Makefile
  include/Makefile
  include/cppunit/Makefile
  include/cppunit/extensions/Makefile
  doc/Makefile
  doc/Doxyfile
  examples/Makefile
  examples/hierarchy/Makefile
  examples/cppunittest/Makefile
],[chmod a+x cppunit-config])

AC_CREATE_PREFIX_CONFIG_H([include/cppunit/config-auto.h], 
        $PACKAGE, [config/config.h])
